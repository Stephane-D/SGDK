plugins {
    id 'java'
    id 'application'
}

group = 'sgdk.tools'
version = '3.97'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':commons')
    implementation project(':apj')
    implementation project(':lz4w')
}

application {
    mainClass = 'sgdk.rescomp.Launcher'
}

tasks.register('fatJar', Jar) {
    archiveClassifier.set('all')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // build classes first
    dependsOn configurations.runtimeClasspath
    dependsOn sourceSets.main.output

    // include compiled classes
    from(sourceSets.main.output)

    // include dependencies (commons + others)
    from({
        configurations.runtimeClasspath.elements.get().collect { file ->
            file.asFile.isDirectory() ? file.asFile : zipTree(file.asFile)
        }
    })

    manifest {
        attributes(
            'Main-Class': application.mainClass.get()
        )
    }
}

tasks.named('build') {
    dependsOn tasks.named('fatJar')
}

tasks.register('install', Copy) {
    dependsOn tasks.named('fatJar')

    def builtJar = tasks.named('fatJar').get().archiveFile.get().asFile
    def sdkBin = file("${rootDir}/../bin")

    from(builtJar)
    into(sdkBin)

    rename { fileName ->
        "rescomp.jar"
    }

    doLast {
        println "Installed rescomp.jar into: ${sdkBin}"
    }
}
